#!/usr/bin/env fish

if [ "$argv[1]" != "" ]
    set time_of_day $argv[1]
else if [ "$DARKMODE" = "1" ]
    set time_of_day "night"
else if [ "$DARKMODE" = "0" ]
    set time_of_day "day"
else if command which osascript > /dev/null
    if [ (osascript -e 'tell app "System Events" to tell appearance preferences to get dark mode') = "true" ]
        set time_of_day "night"
    else
        set time_of_day "day"
    end
else
    set time_of_day (sunshine --simple "!@47.5314 -53.2059")
end

if [ "$time_of_day" = "night" ]
    set upper Dark
    set lower dark
    set dark true
else
    set upper Light
    set lower light
    set dark false
end

if [ (uname -s) = "Darwin" ]
    osascript -e (string join ' ' 'tell app "System Events" to tell appearance preferences to set dark mode to' $dark)
end

if [ -f ~/.config/alacritty/alacritty.toml ]
    sed -i .bak "s/base16-gruvbox-[a-z]*-/base16-gruvbox-$lower-/" ~/.config/alacritty/alacritty.toml
end

if [ -f ~/.config/bat/config ]
    sed -i .bak "s/\"gruvbox-.*\"/\"gruvbox-$lower\"/" ~/.config/bat/config
end

if [ -f ~/.config/starship.toml ]
    sed -i .bak "s/palette = .*/\palette = \"gruvbox_$lower\"/" ~/.config/starship.toml
end

if [ -f ~/.config/wezterm/wezterm ]
    sed -i .bak "s/\"Gruvbox .*\"/\"Gruvbox $upper\"/" ~/.config/wezterm/wezterm.lua
end

if [ -f ~/.config/xfce4/terminal/terminalrc-$lower ]
    cp ~/.config/xfce4/terminal/terminalrc-$lower ~/.config/xfce4/terminal/terminalrc
end

if [ -f ~/.local/vimrc ]
    sed -i .bak "s/^set background=.*/set background=$lower/" ~/.local/vimrc
end

if [ -f (which nvr) ]
    switch (uname)
        case Darwin
            set nvim_control_dirs /var/folders
        case '*'
            set nvim_control_dirs /tmp/nvim.* /tmp/sway.(whoami) /var/run/user/(id -u)

    end

    find $nvim_control_dirs \
        -name 'nvim\.[0-9\.]*' \
        -exec \
            nvim \
            --remote \
            --server {} \
            --remote-send "<esc>:set background=$lower<cr>" \
            \; \
	    2> /dev/null
end

tmux set -g @tmux-gruvbox $lower
tmux source-file ~/.tmux.conf
